plugins {
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'org.openapi.generator' version '7.12.0'
    id 'idea'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // SPRING
//    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // SPRINGDOC
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.3.0'

    // OPENAPI GENERATOR
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    // LOMBOK
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}

test {
    systemProperty 'spring.profiles.active', 'test'
    useJUnitPlatform()
}

bootJar {
    launchScript()
}

openApiGenerate {
    generatorName.set("spring")
    inputSpec.set("$projectDir/src/main/resources/openapi/payments.yml")

    def genDir = layout.buildDirectory.dir("generated")
    outputDir.set(genDir.map { it.asFile.absolutePath })

    modelPackage.set("ru.umd.intershop.payments.web.model")
    apiPackage.set("ru.umd.intershop.payments.web.controller")
    configOptions.set([
            sourceFolder            : "src/main/java",
            interfaceOnly           : "true",
            library                 : "spring-boot",
            reactive                : "true",
            useTags                 : "true",
            typeMappings            : "double=java.math.BigDecimal",
            bigDecimalAsString      : "true",
            skipDefaultInterface    : "true",
            delegatePattern         : "false",
            generateApiDocumentation: "true",
            apiPackage              : "ru.umd.intershop.payments.web.controller",
            modelPackage            : "ru.umd.intershop.payments.web.model",
            openApiNullable         : "true"
    ])
}

sourceSets {
    main {
        java {
            srcDir layout.buildDirectory.dir("generated/src/main/java")
        }
    }
}

compileJava {
    dependsOn 'openApiGenerate'
}

idea {
    module {
        def srcGenJavaDir = file(layout.buildDirectory.dir("generated/src/main/java"))

        sourceDirs += srcGenJavaDir
        generatedSourceDirs += srcGenJavaDir
    }
}